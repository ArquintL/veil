import Mathlib.Tactic
import LeanSts.BFT.Byzantine

/-- Packets over the network. -/
structure NetworkPacket {NetAddr Message : Type} :=
  src : NetAddr
  dst : NetAddr
  msg : Message
  consumed: Bool
deriving DecidableEq

/-- Events within a single node, either internal within a protocol, or
  cross-protocol (input / output). We can think of these as "messages"
  along a channel (in the Go sense). -/
structure ChannelEvent {NetAddr Event : Type} :=
  proc : NetAddr
  tr : Event

abbrev Event (NetAddr Event : Type) := @ChannelEvent NetAddr Event
abbrev Packet (NetAddr Message : Type) := @NetworkPacket NetAddr Message

namespace Packet
private def receive (p : Packet NetAddr Message) : Packet NetAddr Message :=
  { p with consumed := true }

def consume [DecidableEq (Packet NetAddr Message)] (p : Packet NetAddr Message) (soup : Multiset (Packet NetAddr Message)) : Multiset (Packet NetAddr Message) :=
  (soup.erase p) + {receive p}

def broadcast [DecidableEq NetAddr] (src : NetAddr) (dsts : List NetAddr) (msg : Message) : List (Packet NetAddr Message) :=
  dsts.map (λ dst => { src := src, dst := dst, msg := msg, consumed := false })
end Packet

abbrev Network {NetAddr : Type} := List NetAddr

class NetworkProtocol {NetAddr LocalState SelfEvent OutputEvent Message : Type} :=
  localInit : NetAddr → LocalState
  procInternal : @Network NetAddr → LocalState → SelfEvent → LocalState × List (Packet NetAddr Message) × List SelfEvent × List OutputEvent
  procMessage : @Network NetAddr → LocalState → NetAddr /- sender -/ → Message → LocalState × List (Packet NetAddr Message) × List SelfEvent × List OutputEvent

/-- A network consists of the packet soup and the local states of nodes.
  Informally, this is called a "World". -/
structure NetworkState {NetAddr LocalState SelfEvent OutputEvent Packet : Type} :=
  /-- The set of all nodes in the network. -/
  nodes: @Network NetAddr
  /-- The respective local state of nodes. -/
  localState : NetAddr → LocalState

  /-- Channel-like communication within nodes. Input events and internal events. -/
  inputEvents : NetAddr → Multiset SelfEvent
  /-- Channel-like communication within nodes. Output events. -/
  outputEvents : NetAddr → Multiset OutputEvent

  /-- The set of sent messages. -/
  packetSoup : Multiset Packet
  /-- The set of internal events/transitions scheduled to be executed.
      Other events may be issued by the environment; these are events
      generated by the processes themselves, for themself to handle later. -/

@[simp] def updNS {NetAddr LocalState : Type} [DecidableEq NetAddr]
  (n : NetAddr) (st : LocalState) (states : NetAddr → LocalState) : NetAddr → LocalState :=
  λ n' => if n = n' then st else states n'

namespace AsynchronousNetwork
  abbrev World {NetAddr LocalState SelfEvent OutputEvent Packet : Type} := @NetworkState NetAddr LocalState SelfEvent OutputEvent Packet

  def init [protocol : @NetworkProtocol NetAddr LocalState SelfEvent OutputEvent Message] (nodes : @Network NetAddr) :
  (@NetworkState NetAddr LocalState SelfEvent OutputEvent (Packet NetAddr Message)) := {
      nodes := nodes
      localState := protocol.localInit
      inputEvents := fun _ => ∅
      outputEvents := fun _ => ∅
      packetSoup := ∅
    }

  /-- Description of a step being taken. -/
  inductive step {NetAddr SelfEvent Message Packet : Type}
    /-- Stuttering. -/
    | idle
    /-- Packet `p` is delivered to its recipient. -/
    | deliver (p : Packet)
    /-- Node `proc` executes `transition`. -/
    | intern (proc : NetAddr) (transition : SelfEvent)
    /-- The Byzantine adversary creates a packet. -/
    | byz (p : Packet)

  /-- Transition relation of this network. -/
  inductive transition
    [DecidableEq NetAddr] [DecidableEq Message]
    [protocol : @NetworkProtocol NetAddr LocalState SelfEvent OutputEvent Message]
    [byz : @NonadaptiveByzantineAdversary NetAddr (Packet NetAddr Message)
    (@NetworkState NetAddr LocalState SelfEvent OutputEvent (Packet NetAddr Message))]
    (s : @step NetAddr SelfEvent Message (Packet NetAddr Message))
    (w w' : @World NetAddr LocalState SelfEvent OutputEvent (Packet NetAddr Message))
    /-- Stuttering. -/
    | idle (_ : s = step.idle) (_ : w = w')

    /-- Packet `p` is delivered to its recipient. -/
    | deliver (p : (Packet NetAddr Message))
      (_ : s = step.deliver p)
      (_ : p ∈ w.packetSoup)
      (_ : ¬ byz.isByzantine p.dst)
      (_ : let (st', (msgs, _evs)) := protocol.procMessage w.nodes (w.localState p.dst) p.src p.msg
          w' = { w with localState := updNS p.dst st' w.localState,
                        packetSoup := Packet.consume p w.packetSoup + msgs })

    /-- Node `proc` executes internal transition `t`. -/
    | intern (proc : NetAddr) (t : SelfEvent)
      (_ : s = step.intern proc t)
      (_ : ¬ byz.isByzantine proc)
      (_ : let (st', (msgs, _evs)) := protocol.procInternal w.nodes (w.localState proc) t
          w' = { w with localState := updNS proc st' w.localState,
                        packetSoup := w.packetSoup + msgs })

    /-- The Byzantine adversary creates a packet. -/
    | byzantine (p : (Packet NetAddr Message))
      (_ : s = step.byz p)
      /- In Coq, we have an extra assumption:
          `(_ : byz.setting.isByzantine src w)`
        but this should not be part of the semantics itself.
        Instead, we fold it into `canProducePacket`.
      -/
      (_ : byz.constraint.canProducePacket p w)
      (_ : w' = { w with packetSoup := w.packetSoup + {p}})

end AsynchronousNetwork
